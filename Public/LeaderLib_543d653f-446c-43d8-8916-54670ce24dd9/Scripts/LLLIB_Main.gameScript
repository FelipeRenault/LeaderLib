INIT
	FIXEDSTRING:%EmptyString=""
	INT:%EmptyInt=0
	INT:%DefaultShowInTrade=1
	INT:%DefaultAmount=1
	
	STRING:%CharacterIterator_CharacterEvent = ""
	FIXEDSTRING:%CharacterIterator_Tag = ""
	
	STRING:%ItemIterator_ItemEvent = ""
	FIXEDSTRING:%ItemIterator_Tag = ""
	
	STRING:%InventoryIterator_ItemEvent = ""
	FIXEDSTRING:%InventoryIterator_Tag = ""
EVENTS

EVENT LLLIB_AddTreasureTable
VARS
	CHARACTER:_Target
	FIXEDSTRING:_TableName
ON
	OnCharacterEvent(_Target, "LeaderLib_Commands_AddTreasureTable")
ACTIONS
IF "c1"
	GetVar(_TableName, _Target, "LeaderLib_TreasureTableToAdd")
THEN
	CharacterAddTreasureTable(_Target, _TableName)
	CharacterEvent(_Target, "LeaderLib_Events_OnTreasureTableAdded")
	//SetVar(_Target, "LeaderLib_TreasureTableToAdd", %EmptyString)
ENDIF

EVENT LLLIB_RemoveTreasureTable
VARS
	CHARACTER:_Target
	FIXEDSTRING:_TableName
ON
	OnCharacterEvent(_Target, "LeaderLib_Commands_RemoveTreasureTable")
ACTIONS
IF "c1"
	GetVar(_TableName, _Target, "LeaderLib_TreasureTableToRemove")
THEN
	CharacterRemoveTreasureTable(_Target, _TableName)
	CharacterEvent(_Target, "LeaderLib_Events_OnTreasureTableRemoved")
	//SetVar(_Target, "LeaderLib_TreasureTableToRemove", %EmptyString)
ENDIF

EVENT LLLIB_ClearTreasureTables
VARS
	CHARACTER:_Target
	FIXEDSTRING:_TableName
ON
	OnCharacterEvent(_Target, "LeaderLib_Commands_ClearTreasureTables")
ACTIONS
	CharacterClearTreasureTables(_Target)
	CharacterEvent(_Target, "LeaderLib_Events_OnTreasureTablesCleared")
	
EVENT LLLIB_GenerateItemByStat
VARS
	FIXEDSTRING:_StatName
	CHARACTER:_Character
	INT:_ShowInTrade
	INT:_Amount
	FIXEDSTRING:_FlagToSet
ON
	OnCharacterEvent(_Character, "LeaderLib_Commands_CreateItemByStat")
ACTIONS
IF "c1"
	GetVar(_StatName, _Character, "LeaderLib_CreateByStat_NextStatToCreate")
THEN
	IF "c1"
		GetVar(_ShowInTrade, _Character, "LeaderLib_CreateByStat_ShowInTrade")
	THEN
	ELSE
		Set(_ShowInTrade, %DefaultShowInTrade)
	ENDIF
	IF "c1"
		GetVar(_Amount, _Character, "LeaderLib_CreateByStat_AmountToCreate")
	THEN
	ELSE
		Set(_Amount, %DefaultAmount)
	ENDIF
	IF "c1"
		GetVar(_FlagToSet, _Character, "LeaderLib_CreateByStat_FlagToSet")
	THEN
		SetFlag(_Character, _FlagToSet)
	ENDIF
	CharacterAddToInventory(_Character, _StatName, _Amount, _ShowInTrade)
	CharacterEvent(_Character, "LeaderLib_Events_OnItemCreatedWithStat")
	
	IF "c1"
		HasFlag(_Character, "LeaderLib_CreateByStat_ResetVars")
	THEN
		ClearFlag(_Character, "LeaderLib_CreateByStat_ResetVars")
		SetVar(_Character, "LeaderLib_CreateByStat_NextStatToCreate", %EmptyString)
		SetVar(_Character, "LeaderLib_CreateByStat_ShowInTrade", %DefaultShowInTrade)
		SetVar(_Character, "LeaderLib_CreateByStat_AmountToCreate", %DefaultAmount)
		SetVar(_Character, "LeaderLib_CreateByStat_FlagToSet", %EmptyString)
	ENDIF
ENDIF

EVENT LeaderLog_CombatLog
VARS
	CHARACTER:_Character
	FIXEDSTRING:_Text
ON
	OnCharacterEvent(_Character, "LeaderLib_Commands_LogToCombatLog")
ACTIONS
IF "c1"
	GetVar(_Text, _Character, "LeaderLog_CombatLogText")
THEN
	DisplayCombatInfoText(_Character, _Text, 0.0)
	DisplayText(_Character, "", 0.0)
	CharacterEvent(_Character, "LeaderLib_Events_OnCombatLog")
ENDIF

EVENT LeaderLog_RealToString
VARS
	CHARACTER:_Character
	FLOAT:_RealVal
	STRING:_Str
	STRING:_CompletionEvent
ON
	OnCharacterEvent(_Character, "LeaderLog_Commands_RealToString")
ACTIONS
IF "c1&c2"
	GetVar(_RealVal, _Character, "LeaderLog_RealValue")
	GetVar(_CompletionEvent, _Character, "LeaderLog_RealToString_CompletionEvent")
THEN
	Print(_Str, "[1]", _RealVal)
	SetVar(_Character, "LeaderLog_RealString", _Str)
	CharacterEvent(_Character, _CompletionEvent)
ENDIF

EVENT LeaderLib_AutoLevelItems_Start
VARS
	CHARACTER:_Character
ON
	OnCharacterEvent(_Character, "LeaderLib_Events_AutoLevelItems_Start")
ACTIONS
	IterateItemsInInventory(_Character, "LeaderLib_Iterator_AutoLevelItems", "LeaderLib_AutoLevel")

EVENT LeaderLib_AutoLevelItems_Iterate
VARS
	ITEM:_Item
	INT:_Level
	FLOAT:_LevelF
ON
	OnIterateItem(_Item, "LeaderLib_Iterator_AutoLevelItems")
ACTIONS
IF "c1"
	ItemGetStat(_LevelF, _Item, Level)
THEN
	Cast(_Level, _LevelF)
	SetVar(_Item, "LeaderLib_ItemLevel", _Level)
	ItemEvent(_Item, "LeaderLib_Commands_AutoLevelItem")
ENDIF

EVENT LeaderLib_AutoLevelItems_Single
VARS
	CHARACTER:_Character
	ITEM:_Item
	FLOAT:_CharLevel
	FLOAT:_ItemLevel
	INT:_ItemLevelVar
	STRING:_Str
	FIXEDSTRING:_Message
ON
	OnCharacterItemEvent(_Character, _Item, "LeaderLib_Commands_CheckItemLevel")
ACTIONS
IF "c1&c2"
	CharacterGetStat(_CharLevel, _Character, Level)
	ItemGetStat(_ItemLevel, _Item, Level)
THEN
	Cast(_ItemLevelVar, _ItemLevel)
	SetVar(_Item, "LeaderLib_ItemLevel", _ItemLevelVar)
	IF "c1"
		IsLessThen(_ItemLevel, _CharLevel)
	THEN
		CharacterItemEvent(_Character, _Item, "LeaderLib_Commands_AutoLevelItem")
	ENDIF
	CharacterItemEvent(_Character, _Item, "LeaderLib_Events_ItemLevelCheckFinished")
	/*
	Print(_Str, "Item level: [1] | Char Level: [2]", _ItemLevel, _CharLevel)
	Cast(_Message, _Str)
	StatusText(_Character, _Message)
	*/
ENDIF

EVENT LLLIB_CharacterIterator
VARS
	ITEM:_EventParser
ON
	OnItemEvent(_EventParser, "LeaderLib_Commands_StartCharacterIterator")
ACTIONS
IF "c1&c2&!c3"
	GetVar(%CharacterIterator_CharacterEvent, _EventParser, "LeaderLib_IteratorEvent")
	GetVar(%CharacterIterator_Tag, _EventParser, "LeaderLib_IteratorTag")
	IsEqual(%CharacterIterator_CharacterEvent, null)
THEN
	IterateCharacters("LeaderLib_Events_Iterate_OnIterateCharacter", null, null, %CharacterIterator_Tag)
ENDIF

EVENT LLLIB_OnIterateCharacter
VARS
	CHARACTER:_Char
ON
	OnIterateCharacter(_Char, "LeaderLib_Events_Iterate_OnIterateCharacter")
ACTIONS
IF "!c1&!c2"
	IsEqual(%CharacterIterator_CharacterEvent, "")
	IsEqual(%CharacterIterator_CharacterEvent, null)
THEN
	CharacterEvent(_Char, %CharacterIterator_CharacterEvent)
ENDIF

EVENT LLLIB_ItemIterator
VARS
	ITEM:_EventParser
ON
	OnItemEvent(_EventParser, "LeaderLib_Commands_StartItemIterator")
ACTIONS
IF "c1&c2&!c3"
	GetVar(%ItemIterator_ItemEvent, _EventParser, "LeaderLib_IteratorEvent")
	GetVar(%ItemIterator_Tag, _EventParser, "LeaderLib_IteratorTag")
	IsEqual(%ItemIterator_ItemEvent, null)
THEN
	IterateItems("LeaderLib_Events_Iterate_OnIterateItem", %ItemIterator_Tag)
ENDIF

EVENT LLLIB_OnIterateItem
VARS
	ITEM:_Item
ON
	OnIterateItem(_Item, "LeaderLib_Events_Iterate_OnIterateItem")
ACTIONS
IF "!c1&!c2"
	IsEqual(%ItemIterator_ItemEvent, "")
	IsEqual(%ItemIterator_ItemEvent, null)
THEN
	ItemEvent(_Item, %ItemIterator_ItemEvent)
ENDIF

EVENT LLLIB_InventoryIterator_Character
VARS
	CHARACTER:_Target
ON
	OnCharacterEvent(_Target, "LeaderLib_Commands_StartInventoryIterator")
ACTIONS
IF "c1&c2&!c3"
	GetVar(%InventoryIterator_ItemEvent, _Target, "LeaderLib_IteratorEvent")
	GetVar(%InventoryIterator_Tag, _Target, "LeaderLib_IteratorTag")
	IsEqual(%InventoryIterator_ItemEvent, null)
THEN
	IterateItemsInInventory(_Target, "LeaderLib_Events_Iterate_OnIterateItemInInventory", %ItemIterator_Tag)
ENDIF

EVENT LLLIB_InventoryIterator_Item
VARS
	ITEM:_Target
ON
	OnItemEvent(_Target, "LeaderLib_Commands_StartInventoryIterator")
ACTIONS
IF "c1&c2&!c3"
	GetVar(%InventoryIterator_ItemEvent, _Target, "LeaderLib_IteratorEvent")
	GetVar(%InventoryIterator_Tag, _Target, "LeaderLib_IteratorTag")
	IsEqual(%InventoryIterator_ItemEvent, null)
THEN
	IterateItemsInInventory(_Target, "LeaderLib_Events_Iterate_OnIterateItemInInventory", %ItemIterator_Tag)
ENDIF

EVENT LLLIB_InventoryIterator_OnIterateItem
VARS
	ITEM:_Item
ON
	OnIterateItem(_Item, "LeaderLib_Events_Iterate_OnIterateItemInInventory")
ACTIONS
IF "!c1&!c2"
	IsEqual(%InventoryIterator_ItemEvent, "")
	IsEqual(%InventoryIterator_ItemEvent, null)
THEN
	ItemEvent(_Item, %InventoryIterator_ItemEvent)
ENDIF