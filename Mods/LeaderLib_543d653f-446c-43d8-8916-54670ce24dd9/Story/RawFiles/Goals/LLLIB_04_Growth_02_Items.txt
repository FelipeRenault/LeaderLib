Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, _MatchType, _MatchValue)
KBSECTION
//REGION REGISTERING
PROC
LeaderLib_Growth_Items_Register_DeltaMod((STRING)_ID, (INTEGER)_MinLevel, (STRING)_DeltaMod, (STRING)_MatchType, (STRING)_MatchValue)
THEN
DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, _MatchType, _MatchValue);
//END_REGION

//REGION CLEARING
PROC
LeaderLib_Growth_Items_Clear_ByEntriesID((STRING)_ID)
AND
DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, _MatchType, _MatchValue)
THEN
NOT DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, _MatchType, _MatchValue);
//END_REGION

//REGION ITEM_AUTO_LEVEL
QRY
LeaderLib_Growth_Items_QRY_HasAutoLevelItem((CHARACTERGUID)_Character)
AND
NOT DB_LLLIB_Growth_Items_Temp_FoundItem(_Character)
AND
CharacterFindTaggedItem(_Character, "LeaderLib_AutoLevel", _Item)
THEN
LeaderLog_Log("TRACE", "[LeaderLib:Growth:Items:HasAutoLevelItem(CharacterFindTaggedItem)] Found tagged item.");
DB_LLLIB_Growth_Items_Temp_FoundItem(_Character);

//Equipment doesn't return with `CharacterFindTaggedItem`
QRY
LeaderLib_Growth_Items_QRY_HasAutoLevelItem((CHARACTERGUID)_Character)
AND
NOT DB_LLLIB_Growth_Items_Temp_FoundItem(_Character)
AND
DB_LLLIB_EquipmentSlots(_Slot)
AND
NOT DB_LLLIB_Growth_Items_Temp_FoundItem(_Character)
AND
CharacterGetEquippedItem(_Character, _Slot, _Item)
AND
IsTagged(_Item, "LeaderLib_AutoLevel", 1)
THEN
LeaderLog_Log("TRACE", "[LeaderLib:Growth:Items:HasAutoLevelItem] Found tagged equipment in slot [",_Slot,"].");
DB_LLLIB_Growth_Items_Temp_FoundItem(_Character);

QRY
LeaderLib_Growth_Items_QRY_HasAutoLevelItem((CHARACTERGUID)_Character)
AND
DB_LLLIB_Growth_Items_Temp_FoundItem(_Character)
THEN
NOT DB_LLLIB_Growth_Items_Temp_FoundItem(_Character);

IF
CharacterLeveledUp(_Character)
AND
LeaderLib_Growth_Items_QRY_HasAutoLevelItem(_Character)
THEN
LeaderLog_Log("TRACE", "[LeaderLib:Growth:Items:CharacterLeveledUp] Leveling up character equipment.");
SetStoryEvent(_Character, "LeaderLib_Events_AutoLevelItems_Start");

//Equipment doesn't work with `IterateItemsInInventory`
IF
StoryEvent((CHARACTERGUID)_Character, "LeaderLib_Events_AutoLevelItems_Start")
AND
DB_LLLIB_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
AND
IsTagged(_Item, "LeaderLib_AutoLevel", 1)
THEN
CharacterItemSetEvent(_Character, _Item, "LeaderLib_Commands_CheckItemLevel");

IF
StoryEvent((ITEMGUID)_Item, "LeaderLib_Commands_AutoLevelItem")
AND
ItemGetOwner(_Item, _Character)
AND
NOT GetVarInteger(_Item, "LeaderLib_ItemLevel", _)
AND
CharacterGetLevel(_Character, _Level)
THEN
ItemLevelUpTo(_Item, _Level);
SetVarInteger(_Item, "LeaderLib_ItemLevel", _Level);
CharacterItemSetEvent(_Character, _Item, "LeaderLib_Events_ItemLeveledUp");

IF
StoryEvent((ITEMGUID)_Item, "LeaderLib_Commands_AutoLevelItem")
AND
ItemGetOwner(_Item, _Character)
AND
GetVarInteger(_Item, "LeaderLib_ItemLevel", _ItemLevel)
AND
CharacterGetLevel(_Character, _Level)
AND
_ItemLevel < _Level
THEN
ItemLevelUpTo(_Item, _Level);
SetVarInteger(_Item, "LeaderLib_ItemLevel", _Level);
CharacterItemSetEvent(_Character, _Item, "LeaderLib_Events_ItemLeveledUp");

IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Commands_AutoLevelItem")
AND
CharacterGetLevel(_Character, _Level)
THEN
ItemLevelUpTo(_Item, _Level);
SetVarInteger(_Item, "LeaderLib_ItemLevel", _Level);
CharacterItemSetEvent(_Character, _Item, "LeaderLib_Events_ItemLeveledUp");

IF
ItemEquipped(_Item, _Character)
AND
IsTagged(_Item, "LeaderLib_AutoLevel", 1)
THEN
CharacterItemSetEvent(_Character, _Item, "LeaderLib_Commands_CheckItemLevel");
//END_REGION

//REGION DELTA_MODS
QRY
LeaderLib_Growth_Items_QRY_ItemHasDeltaMods((ITEMGUID)_Item)
AND
GetTemplate(_Item, _Template)
AND
DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, "Template", _Template)
THEN
DB_NOOP(1);

QRY
LeaderLib_Growth_Items_QRY_ItemHasDeltaMods((ITEMGUID)_Item)
AND
DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, "Tag", _Tag)
AND
IsTagged(_Item, _Tag, 1)
THEN
DB_NOOP(1);

IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Events_ItemLeveledUp")
THEN
LeaderLib_Growth_Items_ApplyDeltaMods(_Item);

IF
CharacterItemEvent(_Character, _Item, "LeaderLib_Events_ItemLevelCheckFinished")
AND
NOT IsTagged(_Item, "LeaderLib_Growth_AppliedDeltaMods", 1)
AND
LeaderLib_Growth_Items_QRY_ItemHasDeltaMods(_Item)
THEN
LeaderLib_Growth_Items_ApplyDeltaMods(_Item);

PROC
LeaderLib_Growth_Items_ApplyDeltaMods((ITEMGUID)_Item)
AND
GetVarInteger(_Item, "LeaderLib_ItemLevel", _ItemLevel)
AND
DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, "Tag", _Tag)
AND
IsTagged(_Item, _Tag, 1)
AND
_ItemLevel >= _MinLevel
AND
NOT LeaderLib_Helper_QRY_ItemHasDeltaModifier(_Item, _DeltaMod)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth_Items:ApplyDeltaMods] Added deltamod [",_DeltaMod,"] to item.");
LeaderLib_Growth_Items_Internal_ApplyDeltaModTag(_Item);
ItemAddDeltaModifier(_Item, _DeltaMod);

PROC
LeaderLib_Growth_Items_ApplyDeltaMods((ITEMGUID)_Item)
AND
GetVarInteger(_Item, "LeaderLib_ItemLevel", _ItemLevel)
AND
GetTemplate(_Item, _Template)
AND
DB_LLLIB_Growth_Items_DeltaMods(_ID, _MinLevel, _DeltaMod, "Template", _Template)
AND
_ItemLevel >= _MinLevel
AND
NOT LeaderLib_Helper_QRY_ItemHasDeltaModifier(_Item, _DeltaMod)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Growth_Items:ApplyDeltaMods] Added deltamod [",_DeltaMod,"] to item.");
LeaderLib_Growth_Items_Internal_ApplyDeltaModTag(_Item);
ItemAddDeltaModifier(_Item, _DeltaMod);

PROC
LeaderLib_Growth_Items_Internal_ApplyDeltaModTag((ITEMGUID)_Item)
AND
NOT IsTagged(_Item, "LeaderLib_Growth_AppliedDeltaMods", 1)
THEN
SetTag(_Item, "LeaderLib_Growth_AppliedDeltaMods");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
