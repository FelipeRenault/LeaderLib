Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Initializing the mod version here, so settings will use this version
LeaderUpdater_Register_Mod("LeaderLib", "LaughingLeader", "0.9.3.0");
KBSECTION
//REGION UPDATER
PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "System", "DB_LeaderLib_Initialized", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "System", "DB_LeaderLib_GameStarted", 1);
LeaderLog_Log("TRACE", "[LeaderLib:System] Databases registered.");
//END_REGION

//REGION EDITOR_FLAGGING
IF
GameStarted(_, 1)
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");

IF
GameStarted(_, 0)
THEN
GlobalClearFlag("LeaderLib_IsEditorMode");
//END_REGION

//REGION INIT_EVENT
IF
GameStarted(_,_)
THEN
LLLIB_System_ResetInit();
TimerLaunch("LLLIB_Timers_LeaderLibInitialized", 1);

IF
TimerFinished("LLLIB_Timers_LeaderLibInitialized")
THEN
LeaderLog_Log("TRACE", "[LeaderLib:System:TimerFinished(LLLIB_Timers_LeaderLibInitialized)] Running default event [LeaderLib_Initialized].");
DB_LeaderLib_Initialized(1);
GlobalSetFlag("LeaderLib_Initialized");
LeaderLog_Log("DEBUG", "[LeaderLib:ModAPI] LeaderLib initialized.");
LeaderLib_SendEvent("LeaderLib_Initialized");

PROC
LLLIB_System_ResetInit()
THEN
SysClear("DB_LeaderLib_Initialized", 1);
GlobalClearFlag("LeaderLib_Initialized");
//END_REGION

//REGION HOST_FLAGGING
IF
StoryEvent(_,"LeaderLib_Initialized")
THEN
LLLIB_System_MarkHost();

PROC
LLLIB_System_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
UserGetFlag(_Host, "LeaderLib_IsHost", 0)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:System] Flagged host user.");
UserSetFlag(_Host, "LeaderLib_IsHost", 0);

PROC
LLLIB_System_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
DB_IsPlayer(_Player)
AND
_Player != _Host
AND
NOT LeaderLib_Helper_QRY_UserMatch(_Player, _Host)
AND
UserGetFlag(_Player, "LeaderLib_IsHost", 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:System] Cleared host flag from non-host user.");
UserClearFlag(_Player, "LeaderLib_IsHost", 0);

PROC
LLLIB_System_MarkHost()
AND
NOT CharacterGetHostCharacter(_)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:System][ERROR] No host character found.");
//END_REGION

//REGION LEADER_UPDATER_ALIASES
PROC
LeaderUpdater_SaveSettingsVersionData((STRING)_SettingsID)
THEN
LeaderUpdater_SaveSettingsVersionData("LeaderLib", "LaughingLeader", _SettingsID);

QRY
LeaderUpdater_QRY_SettingsNeedsUpdate((STRING)_SettingsID)
AND
LeaderUpdater_QRY_SettingsNeedsUpdate("LeaderLib", "LaughingLeader", _SettingsID)
THEN
DB_NOOP(1);
//END_REGION

//REGION PLAYER_DATABASE_FIX
IF
GameStarted(_,1)
AND
NOT SysIsActive("GLOBAL_CharacterCreation") // If Origins isn't running
THEN
TimerLaunch("LLLIB_Debug_IteratePlayers", 1000);

IF
TimerFinished("LLLIB_Debug_IteratePlayers")
AND
NOT CharacterGetHostCharacter(_)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LLLIB_Debug_IteratePlayers] No host found.");

IF
TimerFinished("LLLIB_Debug_IteratePlayers")
AND
CharacterGetHostCharacter(_Player)
AND
DB_IsPlayer(_Player)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LLLIB_Debug_IteratePlayers] Host already added to DB_IsPlayer.");

IF
TimerFinished("LLLIB_Debug_IteratePlayers")
AND
CharacterGetHostCharacter(_Player)
AND
NOT DB_IsPlayer(_Player)
THEN
DB_IsPlayer(_Player);
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LLLIB_Debug_IteratePlayers] Added host to DB_IsPlayer.");

IF
TimerFinished("LLLIB_Debug_IteratePlayers")
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LLLIB_Debug_IteratePlayers] Searching for players.");
CharacterLaunchIterator("LLLIB_Debug_AddPlayer");

IF
StoryEvent((CHARACTERGUID)_Player, "LLLIB_Debug_AddPlayer")
AND
CharacterIsPlayer(_Player, 1)
AND
NOT DB_IsPlayer(_Player)
THEN
DB_IsPlayer(_Player);
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LLLIB_Debug_IteratePlayers] Added player to DB_IsPlayer.");

IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, "LLLIB_Debug_AddPlayer")
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LLLIB_Debug_IteratePlayers] Iteration complete.");
LeaderLib_SendEvent("LeaderLib_Debug_PlayerDatabaseReady");
//END_REGION

//REGION ITERATOR
PROC
LLLIB_CreateIterator((STRING)_IteratorName, (INTEGER)_MaxInt)
THEN
DB_LLLIB_System_Temp_IteratorCreator(_IteratorName, _MaxInt);
DB_LLLIB_System_Temp_StartIteratorCreator(_IteratorName, 0);

IF
DB_LLLIB_System_Temp_StartIteratorCreator((STRING)_IteratorName, (INTEGER)_CurrentInt)
AND
DB_LLLIB_System_Temp_IteratorCreator(_IteratorName, _MaxInt)
AND
_CurrentInt < _MaxInt
AND
IntegerSum(_CurrentInt, 1, _NextInt)
THEN
DB_LLLIB_Iterator(_IteratorName, _CurrentInt);
NOT DB_LLLIB_System_Temp_StartIteratorCreator(_IteratorName, _CurrentInt);
DB_LLLIB_System_Temp_StartIteratorCreator(_IteratorName, _NextInt);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
