Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Initializing the mod version here, so settings will use this version
LeaderUpdater_Register_Mod("LeaderLib", "LaughingLeader", "1.0.2.0");
LeaderLib_Register_ModMenu("LaughingLeader.LeaderLib", "[LeaderLib] Settings", "LeaderLib_SettingsMenu", "LeaderLib", "LaughingLeader");
LeaderUpdater_Register_ActiveGoal("LeaderLib", "LaughingLeader", "LeaderLib_ZZZ_Updater");
KBSECTION
//REGION EDITOR_FLAGGING
IF
GameStarted(_, 1)
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");

IF
GameStarted(_, 0)
THEN
GlobalClearFlag("LeaderLib_IsEditorMode");
//END_REGION

//REGION INIT_EVENT
IF
GameStarted(_,_)
THEN
LeaderLib_System_ResetInit();
TimerLaunch("LeaderLib_Timers_LeaderLibInitialized", 1);

IF
TimerFinished("LeaderLib_Timers_LeaderLibInitialized")
THEN
LeaderLog_Log("TRACE", "[LeaderLib:System:TimerFinished(LeaderLib_Timers_LeaderLibInitialized)] Running default event [LeaderLib_Initialized].");
DB_LeaderLib_Initialized(1);
GlobalSetFlag("LeaderLib_Initialized");
LeaderLog_Log("DEBUG", "[LeaderLib:ModAPI] LeaderLib initialized.");
LeaderLib_SendEvent("LeaderLib_Initialized");

PROC
LeaderLib_System_ResetInit()
THEN
SysClear("DB_LeaderLib_Initialized", 1);
GlobalClearFlag("LeaderLib_Initialized");
//END_REGION

//REGION HOST_FLAGGING
IF
StoryEvent(_,"LeaderLib_Initialized")
THEN
LeaderLib_System_MarkHost();

PROC
LeaderLib_System_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
UserGetFlag(_Host, "LeaderLib_IsHost", 0)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:System] Flagged host user.");
UserSetFlag(_Host, "LeaderLib_IsHost", 0);

PROC
LeaderLib_System_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
DB_IsPlayer(_Player)
AND
_Player != _Host
AND
NOT LeaderLib_Helper_QRY_UserMatch(_Player, _Host)
AND
UserGetFlag(_Player, "LeaderLib_IsHost", 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:System] Cleared host flag from non-host user.");
UserClearFlag(_Player, "LeaderLib_IsHost", 0);

PROC
LeaderLib_System_MarkHost()
AND
NOT CharacterGetHostCharacter(_)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:System][ERROR] No host character found.");
//END_REGION

//REGION PLAYER_DATABASE_FIX
IF
GameStarted(_,1)
AND
NOT SysIsActive("GLOBAL_CharacterCreation") // If Origins isn't running
THEN
TimerLaunch("LeaderLib_Debug_IteratePlayers", 1000);

IF
TimerFinished("LeaderLib_Debug_IteratePlayers")
AND
NOT CharacterGetHostCharacter(_)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LeaderLib_Debug_IteratePlayers] No host found.");

IF
TimerFinished("LeaderLib_Debug_IteratePlayers")
AND
CharacterGetHostCharacter(_Player)
AND
DB_IsPlayer(_Player)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LeaderLib_Debug_IteratePlayers] Host already added to DB_IsPlayer.");

IF
TimerFinished("LeaderLib_Debug_IteratePlayers")
AND
CharacterGetHostCharacter(_Player)
AND
NOT DB_IsPlayer(_Player)
THEN
DB_IsPlayer(_Player);
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LeaderLib_Debug_IteratePlayers] Added host to DB_IsPlayer.");

IF
TimerFinished("LeaderLib_Debug_IteratePlayers")
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LeaderLib_Debug_IteratePlayers] Searching for players.");
CharacterLaunchIterator("LeaderLib_Debug_AddPlayer");

IF
StoryEvent((CHARACTERGUID)_Player, "LeaderLib_Debug_AddPlayer")
AND
CharacterIsPlayer(_Player, 1)
AND
NOT DB_IsPlayer(_Player)
THEN
DB_IsPlayer(_Player);
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LeaderLib_Debug_IteratePlayers] Added player to DB_IsPlayer.");

IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, "LeaderLib_Debug_AddPlayer")
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Init:LeaderLib_Debug_IteratePlayers] Iteration complete.");
LeaderLib_SendEvent("LeaderLib_Debug_PlayerDatabaseReady");
//END_REGION

//REGION ITERATOR
PROC
LeaderLib_CreateIterator((STRING)_IteratorName, (INTEGER)_MaxInt)
THEN
DB_LeaderLib_System_Temp_IteratorCreator(_IteratorName, _MaxInt);
DB_LeaderLib_System_Temp_StartIteratorCreator(_IteratorName, 0);

IF
DB_LeaderLib_System_Temp_StartIteratorCreator((STRING)_IteratorName, (INTEGER)_CurrentInt)
AND
DB_LeaderLib_System_Temp_IteratorCreator(_IteratorName, _MaxInt)
AND
_CurrentInt < _MaxInt
AND
IntegerSum(_CurrentInt, 1, _NextInt)
THEN
DB_LeaderLib_Iterator(_IteratorName, _CurrentInt);
NOT DB_LeaderLib_System_Temp_StartIteratorCreator(_IteratorName, _CurrentInt);
DB_LeaderLib_System_Temp_StartIteratorCreator(_IteratorName, _NextInt);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
